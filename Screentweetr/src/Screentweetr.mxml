<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute"
				initialize="init(event);" creationComplete="creationComplete(event);">
	<mx:Script>
		<![CDATA[
			import couk.psyked.air.ApplicationConfig;
			import mx.controls.Menu;
			import twitter.types.ServiceTypes;
			import twitter.ServiceManager;
			import couk.psyked.managers.types.EncoderManagerTypes;
			import couk.psyked.managers.EncoderManager;
		//	import couk.mmtdigital.air.ApplicationConfig;
			import air.update.ApplicationUpdaterUI;
			import air.update.events.UpdateEvent;
			import com.pfp.events.JPEGAsyncCompleteEvent;
			import com.pfp.utils.JPEGAsyncEncoder;
			import config.ScreentweetrApplicationConfig;
			import events.ScreentweetrEvent;
			import couk.psyked.air.printscreen.events.PrintScreenMonitorEvent;
			import mx.events.FlexEvent;
			import couk.psyked.air.printscreen.PrintScreenMonitor;
			import mx.core.Window;

			private var notification:NotificationWindow;
			private var settings:SettingsWindow;
			private var help:HelpWindow;
			private static var _bitmapcache:BitmapData;

			public var updater:ApplicationUpdaterUI = new ApplicationUpdaterUI();

			private var screenshotmonitor:PrintScreenMonitor;
			private var dockImage:BitmapData;
			private var _config:ScreentweetrApplicationConfig;
			private var sysMenu:NativeMenu;
			private var stopMenuItem:NativeMenuItem;
			private var startMenuItem:NativeMenuItem;

			public var uploadMessage:String

			private static var _instance:Screentweetr;

			private function init(e:FlexEvent):void
			{
				_instance = this;

				screenshotmonitor = new PrintScreenMonitor(250);
				screenshotmonitor.addEventListener(PrintScreenMonitorEvent.ADD_SCREENSHOT, onScreenshot);
				screenshotmonitor.addEventListener(PrintScreenMonitorEvent.CHANGE_SCREENSHOT, onScreenshot);
				screenshotmonitor.addEventListener(PrintScreenMonitorEvent.REMOVE_SCREENSHOT, removeScreenshot);
				screenshotmonitor.watch();

				_config = ScreentweetrApplicationConfig.getInstance();
				_config.addEventListener(ScreentweetrApplicationConfig.APPLICATION_CONFIG_READY, onSettingsLoaded);
				_config.load();

				EncoderManager.instance.serviceType = EncoderManagerTypes.JPEG;

				NativeApplication.nativeApplication.addEventListener(ScreentweetrEvent.OPEN_SETTINGS, onOpenSettings);
				NativeApplication.nativeApplication.addEventListener(ScreentweetrEvent.CLOSE_SETTINGS, onCloseSettings);
				NativeApplication.nativeApplication.addEventListener(ScreentweetrEvent.OPEN_HELP, onOpenHelp);
				NativeApplication.nativeApplication.addEventListener(ScreentweetrEvent.CLOSE_HELP, onCloseHelp);
				NativeApplication.nativeApplication.addEventListener(ScreentweetrEvent.UPLOAD_IMAGE, onUploadImage);

				updater.configurationFile = File.applicationDirectory.resolvePath("config/updaterConfig.xml");
				updater.addEventListener(UpdateEvent.INITIALIZED, updaterInitialized);
				updater.initialize();
			}

			private function updaterInitialized(e:UpdateEvent):void
			{
				updater.checkNow();
			}

			private function creationComplete(e:Event):void
			{

				var loader:Loader = new Loader();
				loader.contentLoaderInfo.addEventListener(Event.COMPLETE, prepareForSystray);
				if (Capabilities.os.toString().toLowerCase().indexOf("windows") != -1)
				{
					loader.load(new URLRequest("images/Monitor-Dock-16.png"));
				}
				else
				{
					loader.load(new URLRequest("images/iMac-Dock-512.png"));
				}

			}

			public function onSettingsLoaded(e:Event):void
			{

				if (ApplicationConfig.instance.getSetting("showWelcome") != "false")
				{
					callLater(function():void
						{
							NativeApplication.nativeApplication.dispatchEvent(new Event(ScreentweetrEvent.OPEN_HELP));
						});
				}

				if (!ApplicationConfig.instance.getSetting("defaultService"))
				{
					ApplicationConfig.instance.setSetting("defaultService", ServiceTypes.TWEET_PHOTO);
				}

				notification = new NotificationWindow();
				notification.type = NativeWindowType.LIGHTWEIGHT;
				notification.systemChrome = NativeWindowSystemChrome.NONE;
				notification.transparent = true;
				notification.open(false);
				application.dispatchEvent(new Event(ScreentweetrEvent.CLOSE_NOTIFICATION));

			}

			private function onUploadImage(e:Event):void
			{
				trace("Screentweetr onUploadImage");
				EncoderManager.instance.addEventListener(JPEGAsyncCompleteEvent.JPEGASYNC_COMPLETE, onEncodingComplete);
				EncoderManager.instance.addEventListener(ProgressEvent.PROGRESS, onEncodingProgress);
				EncoderManager.instance.encodeBitmapData(bitmapdata);
			}

			private function onEncodingComplete(e:JPEGAsyncCompleteEvent):void
			{
				trace("Screentweetr onEncodingComplete");
				var newFile:File = File.applicationStorageDirectory.resolvePath("screenshot.jpg");
				var newFileStream:FileStream = new FileStream();
				newFileStream.open(newFile, FileMode.WRITE);
				newFileStream.writeBytes(e.ImageData);
				newFileStream.close();
				ServiceManager.instance.uploadToService(newFile, uploadMessage);
			}

			private function onEncodingProgress(e:ProgressEvent):void
			{
				trace("Screentweetr progress event");
				dispatchEvent(e.clone());
			}

			private function onScreenshot(e:Event = null):void
			{
				if (e as PrintScreenMonitorEvent)
				{
					_bitmapcache = (e as PrintScreenMonitorEvent).bitmap.bitmapData;
				}
				else
				{
					//
				}
				application.dispatchEvent(new Event(ScreentweetrEvent.OPEN_NOTIFICATION));
			}

			private function removeScreenshot(e:PrintScreenMonitorEvent):void
			{
				application.dispatchEvent(new Event(ScreentweetrEvent.CLOSE_NOTIFICATION));
			}

			public static function get bitmapdata():BitmapData
			{
				return _bitmapcache;
			}

			private function onOpenSettings(e:Event):void
			{
				if (settings == null)
				{
					settings = new SettingsWindow();
					settings.type = NativeWindowType.UTILITY;
					settings.systemChrome = NativeWindowSystemChrome.ALTERNATE;
					settings.title = "Screentweetr Settings";
					settings.open(true);
					settings.nativeWindow.x = (Screen.mainScreen.visibleBounds.width - settings.width) / 2;
					settings.nativeWindow.y = (Screen.mainScreen.visibleBounds.height - settings.height) / 2;
				}
				else
				{
					settings.orderToFront();
				}
			}

			private function onCloseSettings(e:Event):void
			{
				settings = null;
			}

			private function onOpenHelp(e:Event):void
			{
				if (help == null)
				{
					help = new HelpWindow();
					help.type = NativeWindowType.UTILITY;
					help.systemChrome = NativeWindowSystemChrome.ALTERNATE;
					help.title = "Welcome to Screentweetr";
					help.open(true);
					help.nativeWindow.x = (Screen.mainScreen.visibleBounds.width - help.width) / 2;
					help.nativeWindow.y = (Screen.mainScreen.visibleBounds.height - help.height) / 2;
				}
				else
				{
					help.orderToFront();
				}
			}

			private function onCloseHelp(e:Event):void
			{
				help = null;
			}

			private function createSystrayRootMenu():NativeMenu
			{
				//Add the menuitems with the corresponding actions 
				sysMenu = new NativeMenu();

				var openHelpItem:NativeMenuItem = new NativeMenuItem("Help / Instructions");
				var openSettingsItem:NativeMenuItem = new NativeMenuItem("Application Settings");
				if (Capabilities.os.toString().toLowerCase().indexOf("windows") != -1)
				{
					var exitNativeMenuItem:NativeMenuItem = new NativeMenuItem("Close Screentweetr");
				}
				startMenuItem = new NativeMenuItem("Start Monitoring");
				stopMenuItem = new NativeMenuItem("Stop Monitoring");

				//What should happen when the user clicks on something...       
				openHelpItem.addEventListener(Event.SELECT, onNativeMenuOpenHelp);
				openSettingsItem.addEventListener(Event.SELECT, onNativeMenuOpenSettings);
				if (Capabilities.os.toString().toLowerCase().indexOf("windows") != -1)
				{
					exitNativeMenuItem.addEventListener(Event.SELECT, onNativeMenuCloseMain);
				}
				startMenuItem.addEventListener(Event.SELECT, onStartMonitoring);
				stopMenuItem.addEventListener(Event.SELECT, onStopMonitoring);

				//Add the menuitems to the menu 
				sysMenu.addItem(openHelpItem);
				sysMenu.addItem(openSettingsItem);
				sysMenu.addItem(new NativeMenuItem("", true));
				sysMenu.addItem(stopMenuItem);
				if (Capabilities.os.toString().toLowerCase().indexOf("windows") != -1)
				{
					sysMenu.addItem(new NativeMenuItem("", true));
					sysMenu.addItem(exitNativeMenuItem);
				}

				return sysMenu;
			}

			private function onStartMonitoring(e:Event = null):void
			{
				sysMenu.removeItem(startMenuItem);
				sysMenu.addItemAt(stopMenuItem, 3);
				screenshotmonitor.watch();
				NativeApplication.nativeApplication.dispatchEvent(new Event(ScreentweetrEvent.START_MONITORING));
			}

			private function onStopMonitoring(e:Event = null):void
			{
				sysMenu.removeItem(stopMenuItem);
				sysMenu.addItemAt(startMenuItem, 3);
				screenshotmonitor.unwatch();
				NativeApplication.nativeApplication.dispatchEvent(new Event(ScreentweetrEvent.STOP_MONITORING));
			}

			private function onNativeMenuOpenSettings(e:Event = null):void
			{
				NativeApplication.nativeApplication.dispatchEvent(new Event(ScreentweetrEvent.OPEN_SETTINGS));
			}

			private function onNativeMenuOpenHelp(e:Event = null):void
			{
				NativeApplication.nativeApplication.dispatchEvent(new Event(ScreentweetrEvent.OPEN_HELP));
			}

			private function onNativeMenuCloseMain(e:Event = null):void
			{
				NativeApplication.nativeApplication.exit();
			}

			private function setSystemTrayProperties():void
			{
				if (Capabilities.os.toString().toLowerCase().indexOf("windows") != -1)
				{
					SystemTrayIcon(NativeApplication.nativeApplication.icon).tooltip = "Screentweetr";
				}
				SystemTrayIcon(NativeApplication.nativeApplication.icon).addEventListener(ScreenMouseEvent.CLICK, onScreenshot);
			}

			public function prepareForSystray(event:Event):void
			{
				dockImage = event.target.content.bitmapData;
				if (NativeApplication.supportsSystemTrayIcon)
				{
					setSystemTrayProperties();
					SystemTrayIcon(NativeApplication.nativeApplication.icon).menu = createSystrayRootMenu();
				}
				NativeApplication.nativeApplication.icon.bitmaps = [dockImage];
			}

			public static function get instance():Screentweetr
			{
				if (!_instance)
				{
					_instance = new Screentweetr();
				}
				return _instance;
			}
		]]>
	</mx:Script>
	<mx:Style source="main.css" />
</mx:Application>
